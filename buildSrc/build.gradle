plugins {
    id 'groovy-gradle-plugin'
}

ext {
    micronautVersion = '4.10.4'
    flexmarkVersion = '0.64.8'
}

repositories {
    mavenCentral()
}

dependencies {

    annotationProcessor platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    implementation platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    testAnnotationProcessor platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    testImplementation platform("io.micronaut.platform:micronaut-platform:$micronautVersion")

    annotationProcessor 'io.micronaut:micronaut-inject-java'
    annotationProcessor 'io.micronaut.validation:micronaut-validation-processor'

    implementation 'io.micronaut.validation:micronaut-validation'
    implementation 'io.micronaut:micronaut-http-client'
    implementation 'io.micronaut:micronaut-http'
    implementation 'io.micronaut:micronaut-inject'

    implementation 'org.yaml:snakeyaml'
    implementation 'jakarta.annotation:jakarta.annotation-api'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'io.micronaut.rss:micronaut-rss-core'
    implementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-tables:$flexmarkVersion"

    testImplementation 'io.micronaut:micronaut-http-server-netty'
    testImplementation 'org.spockframework:spock-core'
}

tasks.withType(Jar).configureEach {
    it.duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

gradlePlugin {
    plugins {
        register('grails-website') {
            id = 'grails-website'
            implementationClass = 'website.gradle.GrailsWebsitePlugin'
            displayName = 'Grails Website Plugin'
            description = 'Creates tasks to generate the Grails Website'
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed', 'standardOut', 'standardError')
    }
}